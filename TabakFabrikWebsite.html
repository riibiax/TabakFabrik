<!DOCTYPE html>
<html>
	<head>
		<title>&nbsp;</title>
		<meta charset="utf-8">
		<meta name="keywords" content="Andrea Rovescalli Haute Ecole Geneve cv Portfolio Head Milano Politecnico Design Interaction Art Photo Photography Designer Interactive Ars Electronica Solutions aes aec geopulse Linz Tabak Fabrik" />
		<meta name="description" content=" - "/>
		<meta name="author" content="Rove"/>
		<meta name="copyright" content="Rove"/>
		<meta name="robots" content="index, follow">
		<meta name="viewport" content="width=device-width, minimum-scale=1.0, maximum-scale=1.0">
		<meta property="og:url" content="http://www.rove.ea" />
		<meta property="og:image" content="favicon.ico"/>
		<meta property="og:site_name" content="www.rove.ea" />
		<meta property="og:title" content="&nbsp;" />
		<meta property="og:type" content="website" />
		<meta property="og:description" content="Rove Haute Ecole Geneve cv Portfolio Head Milano Politecnico Design Interaction Art Photo Photography Designer Interactive Ars Electronica Linz" />
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<link rel="shortcut icon" href="favicon.ico" type="image/x-icon"/>
		<link rel="icon" type="image/x-icon" href="favicon.ico" />
		<style type="text/css">
			*, *:before, *:after{
				margin:0;
  				padding:0;
  			}
			html, body{
				cursor: none;
				width: 100%;
				height: 100%;
				min-height: 100%;
			}
			body{
				background: black;
				overflow-x: hidden;
			}
			body::-webkit-scrollbar { 
				display: none; 
			}
			.loader {
			  width: 40px;
			  height: 40px;
			  border-style: solid;
			  border-color: rgba(0,0,0,0);
			  border-top-color: #ffffff;
			  border-width: 1px 1px 0px 0px;
			  position: fixed;
			  top: 50%;
			  left: 50%;
			  transform: translate(-50%, -50%);

			  -webkit-border-radius: 50%;
			     -moz-border-radius: 50%;
			          border-radius: 50%;
			  
			  -webkit-animation: spin 1.0s infinite linear;
			     -moz-animation: spin 1.0s infinite linear;
			       -o-animation: spin 1.0s infinite linear;
			          animation: spin 1.0s infinite linear;
			}

			@-webkit-keyframes spin {
			  from {-webkit-transform: rotate(0deg);}
			  to   {-webkit-transform: rotate(359deg);}
			}

			@-moz-keyframes spin {
			  from {-moz-transform: rotate(0deg);}
			  to   {-moz-transform: rotate(359deg);}
			}

			@-o-keyframes spin {
			  from {-o-transform: rotate(0deg);}
			  to   {-o-transform: rotate(359deg);}
			}

			@keyframes spin{
			  from {transform: rotate(0deg);}
			  to   {transform: rotate(359deg);}
			}

			#c {
				position: fixed;
				left: 0;
				width: 100%;
				height: 100%;
				z-index:-1;
			}

			.list-item {
				display: inline-block;
				vertical-align: top;
				margin: 0;
				padding: 0;
				z-index:-1;
			}
		</style>
	</head>

	<body>
		<canvas id="c"></canvas>
		<div id="container"></div>

		<script id="template" type="notjs">
			<div class="scene"></div>
		</script>
		
		<script src="models/aespoints.js"></script>
		<script src="models/entrancepoints.js"></script>
		<script src="models/entranceexternepoints.js"></script>
		<script src="models/machinepoints.js"></script>
		<script src="models/mainbuildingpoints.js"></script>
		<script src="models/packetpoints.js"></script>
		<script src="models/robotpoints.js"></script>
		<script src="models/tabakCentralpoints.js"></script>
		<script src="models/towerpoints.js"></script>
		<script src="models/trainpoints.js"></script>

		<script src="../../js/threejs/three.min.js"></script>
		<script src="../../js/threejs/Detector.min.js"></script>
		<script src="../../js/threejs/Tween.min.js"></script>

		<script src="../../js/threejs/shaders/CopyShader.min.js"></script>
		<script src="../../js/threejs/shaders/FilmShader.min.js"></script>
		<script src="../../js/threejs/shaders/VignetteShader.min.js"></script>

		<script src="../../js/threejs/postprocessing/EffectComposer.min.js"></script>
		<script src="../../js/threejs/postprocessing/RenderPass.min.js"></script>
		<script src="../../js/threejs/postprocessing/FilmPass.min.js"></script>
		<script src="../../js/threejs/postprocessing/ShaderPass.min.js"></script>
		<script src="../../js/threejs/postprocessing/MaskPass.min.js"></script>

		<script src="../../js/threejs/ImprovedNoise.min.js"></script>

		<script src="TabakFabrikWebsite.js"></script>
		<script src="terrainShader.js"></script>
		<script src="TessellationShader.js"></script>

		<script type="x-shader/x-vertex" id="vertexshader">
			uniform float amplitude;
			uniform float direction;
			uniform vec3 light;
			uniform float time;
			uniform float lightDistance;
			uniform float noiseFactor;
			attribute float size;
			attribute vec3 custompositiona;
			attribute vec3 custompositionb;
			varying vec3 vPosition;
			varying vec3 lightColor;

			void main() {

				vPosition = position;

				// small noise  movement==> è il formicolio che si vede nel disegno==> meno noise è + chiaro è il disegn
				vPosition.x += cos(time + (position.y/8.0))*noiseFactor; 
				vPosition.z += sin(time + (position.x/8.0))*noiseFactor; 
				vPosition.y += sin(time + (position.z/4.0))*noiseFactor;

				// morph
				vec3 morphed = vec3( 0.0, 0.0, 0.0 );
				if (direction == 0.0) {
					morphed += ( custompositionb - position ) * amplitude;
				} else {
					morphed += ( custompositiona - position ) * amplitude;
				}
				morphed += vPosition;

				vec4 mvPosition = modelViewMatrix * vec4( morphed, 1.0 );


				//grandezza delle particelle
				gl_PointSize = min(90.0, size * ( 90.0 / length( mvPosition.xyz ) ) );

				gl_Position = projectionMatrix * mvPosition;

				// light
				vec3 transformedNormal = normalize(mvPosition.xyz);

				//quanto è nera la zona d'ombra. più il valore è basso e + la zona di luce è illuminata e l'altra buia
				lightColor = vec3(0.025,0.025,0.025);

				vec4 lPosition = viewMatrix * vec4( light, 1.0 );
				vec3 lVector = lPosition.xyz - mvPosition.xyz;

				float lDistance = 1.0;
				lDistance = 1.0 - min( ( length( lVector ) / lightDistance ), 1.0 );

				lightColor += vec3(1.0,1.0,1.0) * lDistance;
				lightColor *= 4.0;
			}

		</script>

		<script type="x-shader/x-fragment" id="fragmentshader">
			#define MOD3 vec3(443.8975,397.2973, 491.1871)
			const float UPDATE_RATE = 30.0;
			const float MINIMUM_LIGHT_INTENSITY = 0.75;
			const float MAXIMUM_LIGHT_INTENSITY = 2.75;
			const float LIGHT_INTENSITY_RANDOMNESS = 0.3;
			const float MAXIMUM_LIGHT = MINIMUM_LIGHT_INTENSITY + 2.0 * MAXIMUM_LIGHT_INTENSITY + LIGHT_INTENSITY_RANDOMNESS;
			uniform int lightSwitch;
			uniform sampler2D texture;
			uniform int activateFlickering;
			uniform float time;
			varying vec3 lightColor;
			uniform float lightDistance;
			float hash11(float p)
			{
				vec3 p3  = fract(vec3(p) * MOD3);
   				p3 += dot(p3, p3.yzx + 19.19);
  				return fract(p3.x * p3.y * p3.z);
			}

			void main() {
				float depth = gl_FragCoord.z / gl_FragCoord.w;
				float depthcolor = 1.0 - smoothstep( 0.0, lightDistance, depth );

				vec4 outColor = texture2D( texture, gl_PointCoord );
				if ( outColor.a < 0.5 ) discard; // alpha be gone, più è basso è meno si vede l'alpha ma diventa però giallo. più è alto e più si vede l'alpha==> moche
				if(activateFlickering==1)
				{
					float t = time;
  					t *= UPDATE_RATE;
   					t -= fract(t);
    				t /= UPDATE_RATE;
    				float intensity = (MINIMUM_LIGHT_INTENSITY + (MAXIMUM_LIGHT_INTENSITY + (sin(t) * MAXIMUM_LIGHT_INTENSITY)));
    				intensity += LIGHT_INTENSITY_RANDOMNESS + (hash11(t) * 2.0) - 1.0;
    				intensity /= MAXIMUM_LIGHT;
    				intensity +=0.1;
    				intensity = clamp(intensity, 0.4, 1.0);
    				gl_FragColor = vec4( outColor.xyz * intensity, outColor.a);
				}
				else
				{
					if (lightSwitch != 0) {
						gl_FragColor = vec4( outColor.xyz * lightColor * depthcolor, outColor.a);
					}
					else {
						gl_FragColor = outColor;
					}
				}


			}
		</script>


		<script id="cloudVertexShader" type="x-shader/x-vertex">

        	varying float intensity;
			varying vec2 vUv;

			void main() {

				vUv = uv;
				vec3 vNormal = normalize( normalMatrix * normal );
				intensity = pow(1.0 - abs(dot(vNormal, vec3(0., 0., 1.))), 1.4);
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

			}
		</script>

		<script id="cloudFragmentShader" type="x-shader/x-fragment">
		// Basic edge detection via convolution
		// Ken Slade - ken.slade@gmail.com
		// at https://www.shadertoy.com/view/ldsSWr

		// Based on original Sobel shader by:
		// Jeroen Baert - jeroen.baert@cs.kuleuven.be (www.forceflow.be)
		// at https://www.shadertoy.com/view/Xdf3Rf
			uniform sampler2D map;
			uniform vec2 resolution;
			uniform vec3 fogColor;
			uniform float fogNear;
			uniform float fogFar;
			uniform int activeRay;

			varying vec2 vUv;

			// Use these parameters to fiddle with settings
			#define STEP 0.075

			const mat3 sobelKernelX = mat3(1.0, 0.0, -1.0,
										   2.0, 0.0, -2.0,
										   1.0, 0.0, -1.0);

			const mat3 sobelKernelY = mat3(-1.0, -2.0, -1.0,
										   0.0, 0.0, 0.0,
										   1.0, 2.0, 1.0);
			float convolve(mat3 kernel, mat3 image) {
				float result = 0.0;
				for (int i = 0; i < 3; i++) {
					for (int j = 0; j < 3; j++) {
						result += kernel[i][j]*image[i][j];
					}
				}
				return result;
			}
			vec4 edge(float stepx, float stepy, vec2 center, mat3 kernelX, mat3 kernelY){
				// get samples around pixel
				mat3 image = mat3(length(texture2D(map,center + vec2(-stepx,stepy)).rgb),
								  length(texture2D(map,center + vec2(0,stepy)).rgb),
								  length(texture2D(map,center + vec2(stepx,stepy)).rgb),
								  length(texture2D(map,center + vec2(-stepx,0)).rgb),
								  length(texture2D(map,center).rgb),
								  length(texture2D(map,center + vec2(stepx,0)).rgb),
								  length(texture2D(map,center + vec2(-stepx,-stepy)).rgb),
								  length(texture2D(map,center + vec2(0,-stepy)).rgb),
								  length(texture2D(map,center + vec2(stepx,-stepy)).rgb));
			 	vec2 result;
				result.x = convolve(kernelX, image);
				result.y = convolve(kernelY, image);
			    float color = clamp(length(result), 0.0, 255.0);
			    return vec4(color);
			}
			void main() {
				vec2 uv = vUv;
				float depth = gl_FragCoord.z / gl_FragCoord.w;
				float fogFactor = smoothstep( fogNear, fogFar, depth );
				gl_FragColor = texture2D( map, uv );
				if(activeRay==1){
					gl_FragColor = edge(STEP/resolution.x, STEP/resolution.y, uv, sobelKernelX, sobelKernelY);
				}
				//gl_FragColor.w *= pow( abs(gl_FragCoord.z), 50.0 );
				gl_FragColor = mix( gl_FragColor, vec4( fogColor, gl_FragColor.w ), fogFactor );
			}

		</script>

		<script type="x-shader/x-vertex" id="vertexShaderToy">
			uniform float time;
			varying vec2 vUv;
	        void main() {
	        	vUv = uv;
	            gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
	        }
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderInfinitePerlinNoise">
			// Number of computed scales
			#define NbScales 22.
			// Id of the lowest displayed scale (debug)
			#define FirstScale 0.
			// Anti aliasing
			#define LimitDetails 2.5
			#define SmoothZone 100.
			// Colormap
			#define ClampLevel 1.
			#define ZoomDistance 10.
			// Size of the first Perlin Noise grid (debug)
			#define FirstDivision 8.
			// 0 : multiplicative
			// 1 : additive
			#define Mode 0
			#define GazConcentration 0.
			// Caracteristic ratio of the frequencies (0.5 for octaves)
			#define fRatio .5

			uniform float time;
			uniform vec2 resolution;
			varying vec2 vUv;
			vec2 hash( vec2 p ) {  						// rand in [-1,1]
				p = vec2( dot(p,vec2(127.1,311.7)), dot(p,vec2(269.5,183.3)) );
				return -1. + 2.*fract(sin(p+20.)*53758.5453123);
			}			
			float noise( in vec2 p ) {
    			vec2 i = floor(p), f = fract(p);
				vec2 u = f*f*(3.-2.*f);
    			return mix( mix( dot( hash( i + vec2(0.,0.) ), f - vec2(0.,0.) ), 
                     dot( hash( i + vec2(1.,0.) ), f - vec2(1.,0.) ), u.x),
                	mix( dot( hash( i + vec2(0.,1.) ), f - vec2(0.,1.) ), 
                     dot( hash( i + vec2(1.,1.) ), f - vec2(1.,1.) ), u.x), u.y);
    		}
			void main(void) {
			    vec2 uv = vUv*resolution/ resolution.y;
				float d = 1.; // initial density
				float cycle = cos(mod(-time,100.)/100.*2.*3.14);
				float n_tiles_level_1 = exp(cycle*cycle*ZoomDistance)*pow(2.,FirstDivision);
				// zoom and centering
				uv = (uv - vec2(.9,.5))*n_tiles_level_1 + vec2(.9,.5);
				float theta = 4.+.008*time; // some rotations, not necessary
			    mat2 m = fRatio*mat2( cos(theta),sin(theta), 
								 -sin(theta),cos(theta) );
				// computation of the multiplicative noise
				float q = 1.;
				for (float i = 0.; i < NbScales; i++) {
					if (d<1e-2) continue;
					// multiply the amplitude to maintain the total density
					float c = (i+1.< NbScales) ? 2. : 1.;
					float nn = noise(uv + 10.7*i*i);
					for (float j = 0.; j < GazConcentration; j++) {
						nn = sin(nn*3.14159265359/2.);
					}	
					#if Mode == 0
						float n = c* 0.5*(1.+nn);
					#else
						float n = nn;
					#endif		
					// compute only the visible scales
					float crit = n_tiles_level_1 *q - resolution.x/LimitDetails;
					if (crit < SmoothZone && i >= FirstScale) {
						if (crit>0.) {  // avoid aliasing
							float t = crit/SmoothZone;				
							n = n*(1.-time);
							#if Mode == 0
								n += time;
							#endif
						}			
						#if Mode == 0
							d *= n;
						#else
							d += n*(0.5);
						#endif					
					}
					uv = m*uv; q*= fRatio; // go to the next octave
				}
				d = clamp(d,0.0,d);
				gl_FragColor = vec4(vec3(exp(-d)),1.0);
			}
 		</script>

		<script type="x-shader/x-fragment" id="fragmentShaderHorizontalGradient">
			uniform float time;
			varying vec2 vUv;
			float hash( float n )
			{
			    return fract( sin(n)* 43758.5453123 );
			}
			float noise1( float x )
			{
			    float p = floor(x);
			    float f = fract(x);
			    f = f*f*(3.0-2.0*f);
			    return mix( hash(p+0.0), hash(p+1.0), f );
			}

			float fbm( float p )
			{
			    float f = 0.0;
			    f += 0.5000*noise1( p ); p = p*2.02;
			    f += 0.2500*noise1( p ); p = p*2.03;
			    f += 0.1250*noise1( p ); p = p*2.01;
			    f += 0.0625*noise1( p );
			    return f/0.9375;
			}

			void main(void)
			{
				
				float c = dot( vec3( fbm( vUv.y * 5.134*2.0 + time * 2.013*0.5 ),
						             fbm( vUv.y * 15.002*2.0 + time * 3.591 ),
									 fbm( vUv.y * 25.922*2.0 + time * 4.277 ) ),
							   vec3( .85, .35, .17 ) );
				gl_FragColor = vec4( c, c, c, 1.);
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderFractalNoise">
 			uniform float time;
			varying vec2 vUv;
			uniform vec2 resolution;
			float hash(float x)
			{
				return fract(sin(cos(x*12.13)*19.123)*17.321);
			}
			float noise(vec2 p)
			{
				vec2 pm = mod(p,1.0);
				vec2 pd = p-pm;
				float v0=hash(pd.x+pd.y*41.0);
				float v1=hash(pd.x+1.0+pd.y*41.0);
				float v2=hash(pd.x+pd.y*41.0+41.0);
				float v3=hash(pd.x+pd.y*41.0+42.0);
				v0 = mix(v0,v1,smoothstep(0.0,1.0,pm.x));
				v2 = mix(v2,v3,smoothstep(0.0,1.0,pm.x));
				return mix(v0,v2,smoothstep(0.0,1.0,pm.y));
			}
			void main(void)
			{
				vec2 uv = vUv-0.5;
				uv.x*=resolution.x/resolution.y;
				float v =0.0;
	
				vec2 tuv = uv;
				
				float rot=sin(time*0.3)*sin(time*0.4)*0.2;
					
				uv.x = tuv.x*cos(rot)-tuv.y*sin(rot);
				uv.y = tuv.x*sin(rot)+tuv.y*cos(rot);
				
				for (float i = 0.0; i<12.0; i+=1.0)
				{
					float t = mod(time+i,12.0);
					float l = time-t;
					float e = pow(2.0,t);
					v+=noise(uv*e+vec2(cos(l)*53.0,sin(l)*100.0))*(1.0-(t/12.0))*(t/12.0);
					
				}
				
				v-=0.5;
				v*=1.0;
				
				vec3 color = vec3(v,v,v);
				gl_FragColor = vec4(color,1.0);
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderHash">
 			#define ITERATIONS 4
 			uniform float time;
			varying vec2 vUv;
			uniform vec2 resolution;
			float hash(vec2 p)
			{
				return fract(sin(dot(p, vec2(12.9898, 78.233))) * 43758.5453);
			}
			void main( void)
			{
				vec2 position = resolution* vUv;
				float b = 0.0;
			    for (int t = 0; t < ITERATIONS; t++)
			    {
			        float v = float(t+1)*.152;
			        vec2 pos = (position * v + time * 5000. + 1.0);
			    	b += hash(pos);
			    }
			    vec3 col = vec3(mix(b,b, step(vUv.x, .5))) / float(ITERATIONS);
				gl_FragColor = vec4(col, 1.0);
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderMovingNoise">
 			uniform float time;
 			varying vec2 vUv;
			uniform vec2 resolution;
			vec3 mod289(vec3 x) {
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec4 mod289(vec4 x) {
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec4 permute(vec4 x) {
			     return mod289(((x*34.0)+1.0)*x);
			}

			vec4 taylorInvSqrt(vec4 r)
			{
			  return 1.79284291400159 - 0.85373472095314 * r;
			}

			float snoise(vec3 v)
			  { 
			  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
			  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

			// First corner
			  vec3 i  = floor(v + dot(v, C.yyy) );
			  vec3 x0 =   v - i + dot(i, C.xxx) ;

			// Other corners
			  vec3 g = step(x0.yzx, x0.xyz);
			  vec3 l = 1.0 - g;
			  vec3 i1 = min( g.xyz, l.zxy );
			  vec3 i2 = max( g.xyz, l.zxy );

			  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
			  //   x1 = x0 - i1  + 1.0 * C.xxx;
			  //   x2 = x0 - i2  + 2.0 * C.xxx;
			  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
			  vec3 x1 = x0 - i1 + C.xxx;
			  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
			  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

			// Permutations
			  i = mod289(i); 
			  vec4 p = permute( permute( permute( 
			             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
			           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
			           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

			// Gradients: 7x7 points over a square, mapped onto an octahedron.
			// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
			  float n_ = 0.142857142857; // 1.0/7.0
			  vec3  ns = n_ * D.wyz - D.xzx;

			  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

			  vec4 x_ = floor(j * ns.z);
			  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

			  vec4 x = x_ *ns.x + ns.yyyy;
			  vec4 y = y_ *ns.x + ns.yyyy;
			  vec4 h = 1.0 - abs(x) - abs(y);

			  vec4 b0 = vec4( x.xy, y.xy );
			  vec4 b1 = vec4( x.zw, y.zw );

			  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
			  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
			  vec4 s0 = floor(b0)*2.0 + 1.0;
			  vec4 s1 = floor(b1)*2.0 + 1.0;
			  vec4 sh = -step(h, vec4(0.0));

			  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
			  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

			  vec3 p0 = vec3(a0.xy,h.x);
			  vec3 p1 = vec3(a0.zw,h.y);
			  vec3 p2 = vec3(a1.xy,h.z);
			  vec3 p3 = vec3(a1.zw,h.w);

			//Normalise gradients
			  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
			  p0 *= norm.x;
			  p1 *= norm.y;
			  p2 *= norm.z;
			  p3 *= norm.w;

			// Mix final noise value
			  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
			  m = m * m;
			  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
			                                dot(p2,x2), dot(p3,x3) ) );
			  }

			void main(void)
			{
				vec2 uv = vUv*vec2(resolution.x/resolution.y,1.0);
				float f = 0.0;
				{
					uv *= 1.25;
			        mat2 m = mat2( 1.6,  1.2, -1.2,  1.6 );
					f  = 0.75000*snoise( vec3(uv , time/10.0)); uv = m*uv;
					f += 0.5000*snoise( vec3(uv , time/10.0)); uv = m*uv;
					f += 0.5000*snoise( vec3(uv , time/10.0)); uv = m*uv;
					f += 0.2500*snoise( vec3(uv , time/5.0)); uv = m*uv;
					f += 0.1250*snoise( vec3(uv ,time/5.0)); uv = m*uv;
					f += 0.0625*snoise( vec3(uv ,time/2.0)); uv = m*uv;
					f += 0.015725*snoise( vec3(uv ,time/2.0)); uv = m*uv;
					f += 0.00125*snoise( vec3(uv ,time/2.0)); uv = m*uv;
					f += 0.000525*snoise( vec3(uv ,time/2.0)); uv = m*uv;
				}
				f = 0.5 + 0.5*f;
				gl_FragColor = vec4( f, f, f, 1.0 );
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderStripesNoise">
 			uniform float time;
			varying vec2 vUv;
			uniform vec2 resolutionImage;
			uniform sampler2D iChannel1;
			uniform sampler2D iChannel2;
			vec4 blend(vec4 a, vec4 b, float t)
			{
				return (a*(1.-t))+(b*t);
			}
			void main(void)
			{
			    float newtime = time*.1;
			    const float bias = 0.1;
			    const float flash = 0.5;
			    const float strip_size = 50.;
			    vec2 uv_video   = vUv;
				vec2 uv_static  = vUv;
			    float v_sample = newtime*flash;
			    uv_static.y = v_sample;
			    vec2 uv_bars  = vUv;
			    uv_bars.y = v_sample;
			    uv_bars -= mod(uv_bars,vec2(1.0)/(resolutionImage.xy*(10./strip_size)));
				vec4 noise_color = 
			        blend(
			            texture2D(iChannel1, uv_bars),
			            texture2D(iChannel2, uv_static),
			            bias
			        );
			    gl_FragColor = 1.-noise_color;
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderCartesianGrid">
 			uniform float time;
 			uniform vec2 resolution;
			varying vec2 vUv;
			const float M_PI    = 3.14159265358979323846;
			const float M_SQRT2 = 1.41421356237309504880;
			// Forward cartesian projection
			vec2 transform_forward(vec2 P)
			{
			    return P;
			}
			// Inverse cartesian projection
			vec2 transform_inverse(vec2 P)
			{
			    return P;
			}
			// ------------------------------------------------
			vec2 scale_forward(vec2 P, vec4 limits)
			{
			    // limits = xmin,xmax,ymin,ymax
			    P += vec2(.5,.5);
			    P *= vec2(limits[1] - limits[0], limits[3]-limits[2]);
			    P += vec2(limits[0], limits[2]);
			    return P;
			}
			// ------------------------------------------------
			vec2 scale_inverse(vec2 P, vec4 limits)
			{
			    // limits = xmin,xmax,ymin,ymax
			    P -= vec2(limits[0], limits[2]);
			    P /= vec2(limits[1]-limits[0], limits[3]-limits[2]);
			    return P - vec2(.5,.5);
			}
			// Antialias stroke alpha coeff
			float stroke_alpha(float distance, float linewidth, float antialias)
			{
			    float t = linewidth/2.0 - antialias;
			    float signed_distance = distance;
			    float border_distance = abs(signed_distance) - t;
			    float alpha = border_distance/antialias;
			    alpha = exp(-alpha*alpha);
			    if( border_distance > (linewidth/2.0 + antialias) )
			        return 0.0;
			    else if( border_distance < 0.0 )
			        return 1.0;
			    else
			        return alpha;
			}
			// Compute the nearest tick from a (normalized) t value
			float get_tick(float t, float vmin, float vmax, float step)
			{
			    float first_tick = floor((vmin + step/2.0)/step) * step;
			    float last_tick = floor((vmax + step/2.0)/step) * step;
			    float tick = vmin + t*(vmax-vmin);
			    if (tick < (vmin + (first_tick-vmin)/2.0))
			        return vmin;
			    if (tick > (last_tick + (vmax-last_tick)/2.0))
			        return vmax;
			    tick += step/2.0;
			    tick = floor(tick/step)*step;
			    return min(max(vmin,tick),vmax);
			}
			void main(void)
			{
			 	// Cartesian limits
				vec4 u_limits1 = vec4(-10.1, +10.1, -10.1, +10.1)  * .5*(1.+sin(time/2.))/2.;
				// Projected limits
				vec4 u_limits2 = vec4(-10.,10.,-10.,10.);
			    const float u_antialias = 1.0;
				vec2 u_major_grid_step = vec2(.5, .5);
				vec2 u_minor_grid_step = vec2(.1, .1);
				float u_major_grid_width = 2.00;
				float u_minor_grid_width = 1.00;
				vec4  u_major_grid_color = vec4(0,0,0,1);
				vec4  u_minor_grid_color = vec4(.5,.5,.5,1);
			    vec2 v_texcoord;
			    if (resolution.x > resolution.y) {
			        v_texcoord.x = (vUv*resolution).x/resolution.y - 0.5
			                     - 0.5*(resolution.x-resolution.y)/resolution.y;
			        v_texcoord.y = (vUv*resolution).y/resolution.y - 0.5;
				} else {	
			        v_texcoord.x = (vUv*resolution).x/resolution.x - 0.5;
			        v_texcoord.y = (vUv*resolution).y/resolution.x - 0.5
			                     - 0.5*(resolution.y-resolution.x)/resolution.x;
			    }
			    vec2 v_size = resolution.xy;
			    vec2 NP1 = v_texcoord;
			    vec2 P1 = scale_forward(NP1, u_limits1);
			    vec2 P2 = transform_inverse(P1);
			    bvec2 outside = bvec2(false);
			    if( P2.x < u_limits2[0] ) outside.x = true;
			    if( P2.x > u_limits2[1] ) outside.x = true;
			    if( P2.y < u_limits2[2] ) outside.y = true;
			    if( P2.y > u_limits2[3] ) outside.y = true;
			    vec2 NP2 = scale_inverse(P2,u_limits2);
			    vec2 P;
			    float tick;
			    tick = get_tick(NP2.x+.5, u_limits2[0], u_limits2[1], u_major_grid_step[0]);
			    P = transform_forward(vec2(tick,P2.y));
			    P = scale_inverse(P, u_limits1);
			    float Mx = length(v_size * (NP1 - P));
			    tick = get_tick(NP2.x+.5, u_limits2[0], u_limits2[1], u_minor_grid_step[0]);
			    P = transform_forward(vec2(tick,P2.y));
			    P = scale_inverse(P, u_limits1);
			    float mx = length(v_size * (NP1 - P));
			    tick = get_tick(NP2.y+.5, u_limits2[2], u_limits2[3], u_major_grid_step[1]);
			    P = transform_forward(vec2(P2.x,tick));
			    P = scale_inverse(P, u_limits1);
			    float My = length(v_size * (NP1 - P));
			    tick = get_tick(NP2.y+.5, u_limits2[2], u_limits2[3], u_minor_grid_step[1]);
			    P = transform_forward(vec2(P2.x,tick));
			    P = scale_inverse(P, u_limits1);
			    float my = length(v_size * (NP1 - P));
			    float M = min(Mx,My);
			    float m = min(mx,my);
			    // Here we take care of "finishing" the border lines
			    if( outside.x && outside.y ) {
			        if (Mx > 0.5*(u_major_grid_width + u_antialias)) {
			            gl_FragColor = vec4(1); return;
			        } else if (My > 0.5*(u_major_grid_width + u_antialias)) {
			            gl_FragColor = vec4(1); return;
			        } else {
			            M = max(Mx,My);
			        }
			    } else if( outside.x ) {
			        if (Mx > 0.5*(u_major_grid_width + u_antialias)) {
			            gl_FragColor = vec4(1); return;
			        } else {
			            M = m = Mx;
			        }
			    } else if( outside.y ) {
			        if (My > 0.5*(u_major_grid_width + u_antialias)) {
			            gl_FragColor = vec4(1); return;
			        } else {
			            M = m = My;
			        }
			    }
			    // Mix major/minor colors to get dominant color
			    vec4 color = u_major_grid_color;
			    float alpha1 = stroke_alpha( M, u_major_grid_width, u_antialias);
			    float alpha2 = stroke_alpha( m, u_minor_grid_width, u_antialias);
			    float alpha  = alpha1;
			    if( alpha2 > alpha1*1.5 )
			    {
			        alpha = alpha2;
			        color = u_minor_grid_color;
			    }

			    gl_FragColor = mix(vec4(1,1,1,1), color, alpha);
			}
 		</script>

 		<script type="x-shader/x-fragment" id="fragmentShaderScanline">
 			uniform float time;
			uniform sampler2D iChannel1;
			const float speed = 7.;
			varying vec2 vUv;

			void main(void)
			{
			    float newtime = time*speed;
			    float f = 1.-floor(newtime)*4.+(1.-vUv.y)*300.;
			    f = clamp (f,0.,1.);
			    gl_FragColor = texture2D(iChannel1, vUv );
			    float f2 = (fract(newtime)>vUv.x)?1.:0.;
			    vec4 rowColor = mix(vec4(0.), gl_FragColor, f2);
			    gl_FragColor = mix(gl_FragColor, rowColor, f);

			    f = 1.-floor(newtime+1.)*4.+(1.-vUv.y)*300.;
			    f = clamp (f,0.,1.);

			    gl_FragColor = mix(gl_FragColor, vec4(0.), f);
			}
 		</script>

 		<script type="x-shader/x-vertex" id="spaceVertexShader">
			uniform float delta;
			uniform float strength;
			attribute float size;
			varying vec3 vColor;
			varying vec3 vNormal;

			const vec3 axisx = vec3(1.0, 0.0, 0.0);
			const vec3 axisy = vec3(0.0, 1.0, 0.0);
			const vec3 axisz = vec3(0.0, 0.0, 1.0);

			const vec3 center = vec3(0.0, 0.0, 0.0);

			void main() {
				vec3 directionVec = normalize(position);

				float xangle = dot(axisx, directionVec) * 5.0;
				float yangle = dot(axisy, directionVec) * 6.0;
				float zangle = dot(axisz, directionVec) * 4.5;
				vec3 animated = position;

				float cosx = cos(delta + xangle);
				float sinx = sin(delta + xangle);
				float cosy = cos(delta + yangle);
				float siny = sin(delta + yangle);
				float cosz = cos(delta + zangle);
				float sinz = sin(delta + zangle);

				animated.x += directionVec.x * cosx * siny * cosz * strength;
				animated.y += directionVec.y * sinx * cosy * sinz * strength;
				animated.z += directionVec.z * sinx * cosy * cosz * strength;

				vec4 mvPosition = modelViewMatrix * vec4( animated, 1.0 );
				gl_PointSize = size * ( 200.0 / -mvPosition.z );
				gl_Position = projectionMatrix * mvPosition;

				float dx = center.x - animated.x;
				float dy = center.y - animated.y;
				float dz = center.z - animated.z;

				float distance = dx * dx + dy * dy + dz * dz;
				vColor = vec3(1.0,1.0,1.0) * (distance*0.00007);

				vNormal = normal;
			}
		</script>

		<script type="x-shader/x-fragment" id="spaceFragmentShader">
			uniform float lightNear;
			uniform float lightFar;
			uniform sampler2D texture;
			varying vec3 vColor;
			varying vec3 vNormal;

			void main() {
				vec4 textureColor = texture2D( texture, gl_PointCoord );
				float depth = gl_FragCoord.z / gl_FragCoord.w;
				float depthcolor = 1.0 - smoothstep( lightNear, lightFar, depth );

				vec3 light1 = vec3(0.5,0.5,-1.0);
				light1 = normalize(light1);
				float d1 = max(0.4,dot(vNormal, light1))*3.0;

				vec3 light2 = vec3(-0.5,-0.5,1.0);
				light2 = normalize(light2);
				float d2 = max(0.4,dot(vNormal, light2))*3.0;

				d1 *= d2;
				gl_FragColor = vec4( (textureColor.xyz * vColor * depthcolor) * d1 , textureColor.a );
			}
		</script>



		<script type="x-shader/x-fragment" id="spaceWireFragmentShader">
			uniform float lightNear;
			uniform float lightFar;
			varying vec3 vColor;
			varying vec3 vNormal;
			void main() {
				float depth = gl_FragCoord.z / gl_FragCoord.w;
				float near=lightNear + 15.0;
			    float far= lightFar  - 25.0;
				float depthcolor = 1.0 - smoothstep( near, far, depth );

				vec3 light1 = vec3(0.5,0.5,-1.0);
				light1 = normalize(light1);
				float d1 = max(0.4,dot(vNormal, light1))*3.0;

				vec3 light2 = vec3(-0.5,-0.5,1.0);
				light2 = normalize(light2);
				float d2 = max(0.4,dot(vNormal, light2))*3.0;

				d1 *= d2;
				float color = 0.0;
				color += sin( vNormal.x * cos( depth / 350.0 ) * 800.0 ) + cos( vNormal.y * cos( depth / 350.0 ) * 200.0 );
				color += sin( vNormal.y * sin( depth / 150.0 ) * 400.0 ) + cos( vNormal.x * sin( depth / 450.0 ) * 400.0 );
				gl_FragColor = vec4( ( vColor * depthcolor) * d1*color, 0.1*color );
			}

		</script>

		<script id="robotVertexShader" type="x-shader/x-vertex">
			uniform float fresnelBias;
			uniform float fresnelScale;
			uniform float fresnelPower;

			varying float vReflectionFactor;
			varying vec3 vReflect;
			uniform float globalTime;

			void main() {
			    vec3 newPosition = position;

        		newPosition.x += cos(globalTime + (position.y/8.0))*2.0; 
        		newPosition.z += sin(globalTime + (position.x/4.0))*4.0; 
        		//newPosition.y += sin(globalTime + (position.z/4.0))*2.0;
  				newPosition = position + newPosition *0.05;

  				vec4 mvPosition = modelViewMatrix * vec4( newPosition, 1.0 );
  				vec4 worldPosition = modelMatrix * vec4( newPosition, 1.0 );

			  vec3 worldNormal = normalize( mat3( modelMatrix[0].xyz, modelMatrix[1].xyz, modelMatrix[2].xyz ) * normal );

			  vec3 I = worldPosition.xyz - cameraPosition;
			  
			  vReflect = reflect( I, worldNormal );
			  vReflectionFactor = fresnelBias + fresnelScale * pow( 1.0 + dot( normalize( I ), worldNormal ), fresnelPower );

			  gl_Position = projectionMatrix * mvPosition;
			}
		</script>
			  
		<script id="robotFragmentShader" type="x-shader/x-fragment">
			uniform vec3 color;
			uniform samplerCube envMap;

			varying vec3 vReflect;
			varying float vReflectionFactor;

			void main() {
			  vec4 envColor = textureCube( envMap, vec3( -vReflect.x, vReflect.yz ) );
			  gl_FragColor = vec4(mix(color, envColor.xyz, vec3(clamp( vReflectionFactor, 0.0, 1.0 ))), 1.0);
			}
		</script>


		<div id="loading_spinner" class="loader"></div>

		<script>
			if ( ! Detector.webgl ) Detector.addGetWebGLMessage();
			TabakFabrik.init();
		</script>
	</body>
</html>
